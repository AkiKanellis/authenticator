def configsFolder = 'scripts/configs'

plugins.apply('checkstyle')

checkstyle {
    toolVersion = '6.16.1'
}

task checkstyle(type: Checkstyle) {
    ignoreFailures false
    showViolations true

    source 'src'
    include '**/*.java'
    configFile rootProject.file("$configsFolder/checkstyle_config.xml")

    classpath = files()
}

plugins.apply('pmd')

pmd {
    toolVersion = '5.4.1'
}

task pmd(type: Pmd) {
    ignoreFailures = false

    ruleSets = []

    source = fileTree('src/main/java')
    ruleSetFiles = rootProject.files("$configsFolder/pmd_ruleset.xml")
}

plugins.apply('findbugs')

task findbugs(type: FindBugs) {
    ignoreFailures = false
    effort = 'max'
    reportLevel = 'low'

    reports {
        xml {
            enabled = false
        }
        html {
            enabled = true
        }
    }

    classes = files("${project.projectDir}/build/classes")
    source = fileTree('src/main/java')
    excludeFilter = rootProject.file("$configsFolder/findbugs_filter.xml")

    classpath = files()
}

afterEvaluate {
    tasks.findByName('pmd').dependsOn('assemble')
    tasks.findByName('findbugs').dependsOn('assemble')

    def checkTask = tasks.findByName('check')

    checkTask.dependsOn('pmd')
    checkTask.dependsOn('findbugs')
    checkTask.dependsOn('checkstyle')
}